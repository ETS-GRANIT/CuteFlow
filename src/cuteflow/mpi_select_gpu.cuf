module mpi_select_gpu
  interface
    subroutine quicksort(base, nmemb, elemsize, compar) &
        bind(C,name='qsort') 
      use iso_c_binding
      implicit none
      !pgi$ ignore_tkr base,nmemb,elemsize,compar
      type(C_pTR), value :: base
      integer(C_SIZE_T), value :: nmemb, elemsize
      type(C_FUNPTR), value :: compar
    end subroutine quicksort

    integer function strcmp(a,b) bind(C,name='strcmp')
      use iso_c_binding
      implicit none
      !pgi$ ignore_trk a,b
      type(C_PTR), value :: a, b
    end function strcmp
  end interface

contains
  subroutine assignDevice(dev)
    use mpi
    use cudafor
    implicit none
    integer :: dev
    character (len=MPI_MAX_PROCESSOR_NAME), allocatable :: hosts(:)
    character (len=MPI_MAX_PROCESSOR_NAME) :: hostname
    integer :: namelenght, color, i
    integer :: nProcs, myrank, newComm, newRank, ierr

    call MPI_COMM_SIZE(MPI_COMM_WORLD, nProcs, ierr)
    call MPI_COMM_RANK(MPI_COMM_WORLD, myrank, ierr)

    allocate(hosts(0:nProcs-1))

    call MPI_GET_PROCESSOR_NAME(hostname, namelenght, ierr)
    hosts(myrank) = hostname(1:namelenght)
    do i=0,nProcs-1
      call MPI_BCAST(hosts(i),MPI_MAX_PROCESSOR_NAME, &
        MPI_CHARACTER,i,MPI_COMM_WORLD,ierr)
    end do

    call quicksort(hosts,nProcs,MPI_MAX_PROCESSOR_NAME,strcmp)

    color=0
    do i=0,nProcs-1
      if(i>0) then
        if(lne(hosts(i-1),hosts(i))) color=color+1
      end if
      if(leq(hostname,hosts(i))) exit
    end do  

    call MPI_COMM_SPLIT(MPI_COMM_WORLD,color,0,newComm,ierr)
    call MPI_COMM_RANK(newComm,newRank,ierr)

    dev = newRank
    ierr = cudaSetDevice(dev)

    deallocate(hosts)
  end subroutine assignDevice


  function leq(s1,s2) result(res)
    implicit none
    character (len=*) :: s1, s2
    logical :: res
    res = .FALSE.
    if(lle(s1,s2) .and. lge(s1,s2)) res= .TRUE.
  end function leq

  function lne(s1, s2) result(res)
    implicit none
    character(len=*) :: s1, s2
    logical :: res
    res = .not. leq(s1,s2)
  end function
end module mpi_select_gpu
