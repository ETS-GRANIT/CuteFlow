module pre_post_traitement
  implicit none
contains

  subroutine global_save_sol_visu
    use precision_kind
    use global_data
    use global_data_device
    use file_id
    implicit none

    integer :: ierr

    !! Stockage de la solution pour postraitement et animation
    if(solvisu > 0 .and. cptsolvisu<=visu_snapshots)then
      if(cptsolvisu>0) tolaffiche=1.5*dt

      if(( tc <= tsolvisu(cptsolvisu) + tolaffiche) .and. (tc >= tsolvisu(cptsolvisu) - tolaffiche))then
        vdlg = vdlg_d

        !Praview EXPORT
        if(solvisu==1 .or. solvisu==2) then
          call paraview_export(cptsolvisu)
        end if

        !CGNS EXPORT
        if(is_cgns==1 .and. (solvisu==1 .or. solvisu==3)) then
          if(cptsolvisu==0) call cgns_export_init
          call cgns_export(cptsolvisu)
          if(cptsolvisu==visu_snapshots) call cgns_export_end
        end if

        !Simple EXPORT
        if(solvisu==1 .or. solvisu==4) then
          if(.not. is_solnode) then
            vdlg_nodes = 0.
            call compute_sol_on_nodes
            is_solnode = .TRUE.
          end if
          call simple_export(cptsolvisu)
        end if
        cptsolvisu = cptsolvisu + 1
      else if( tc > tsolvisu(cptsolvisu) + tolaffiche ) then
        print*,"Skipped save sol ! : The timestep is too large for the number of snapshots to save!" ,cptsolvisu
        cptsolvisu = cptsolvisu+1
      end if
    endif

  end subroutine global_save_sol_visu

  subroutine compute_sol_on_nodes
    use precision_kind
    use global_data

    real(fp_kind), dimension(nnt) :: area
    integer :: i, j, jj

    area = 0.

    do i=1,nelt
      do j=1,ndln
        jj = connectivite(i,j)
        vdlg_nodes(:,jj) = vdlg_nodes(:,jj) + vdlg(:,i)*surf(i)
        area(jj) = area(jj) + surf(i)
      end do
    end do

    do jj=1,nnt
      vdlg_nodes(:,jj) = vdlg_nodes(:,jj)/area(jj)
    end do
  end subroutine compute_sol_on_nodes

  subroutine ouv_fichiers
    use precision_kind
    use global_data
    use file_id
    implicit none

    if(num_mpi_process>1) then
      sol_elt_ts = trim(adjustl(mpi_process_id_string))//trim(adjustl('_solution_elements_restart.txt'))
    else
      sol_elt_ts = trim(adjustl('solution_elements_restart.txt'))
    end if

    if(solrestart==1) then
      open(ec_sol_elt_t, file=sol_elt_ts, status="unknown")
    endif
  end subroutine ouv_fichiers

  subroutine bluekenue_export
    use precision_kind
    use global_data
    use file_id

    implicit none

    integer :: nd, iel
    real(fp_kind) :: solu, solv

    write(valtemps,'(f12.3)') tc 

    if(num_mpi_process>1) then
      solfile_eta_T3S = trim(adjustl(mpi_process_id_string)) // '_solution-ETA-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
      solfile_h_T3S   = trim(adjustl(mpi_process_id_string)) //   '_solution-H-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
      solfile_u_T3S   = trim(adjustl(mpi_process_id_string)) //   '_solution-U-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
      solfile_v_T3S   = trim(adjustl(mpi_process_id_string)) //   '_solution-V-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
    else
      solfile_eta_T3S = 'solution-ETA-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
      solfile_h_T3S   = 'solution-H-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
      solfile_u_T3S   = 'solution-U-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
      solfile_v_T3S   = 'solution-V-Bluekenue_t_'// trim(adjustl(valtemps)) // 's.T3S'
    end if

    open(unit=ec_Bluekenu_eta,file=solfile_ETA_T3S,status="unknown")
    open(unit=ec_Bluekenu_h,file=solfile_H_T3S,    status="unknown")
    open(unit=ec_Bluekenu_u,file=solfile_U_T3S,    status="unknown")
    open(unit=ec_Bluekenu_v,file=solfile_V_T3S,    status="unknown")

    write(ec_Bluekenu_eta,*) ':NodeCount', nnt
    write(ec_Bluekenu_eta,*) ':ElementCount', nelt
    write(ec_Bluekenu_eta,*) ':ElementType  T3'
    write(ec_Bluekenu_eta,*) ':EndHeader'

    write(ec_Bluekenu_h,*) ':NodeCount', nnt
    write(ec_Bluekenu_h,*) ':ElementCount', nelt
    write(ec_Bluekenu_h,*) ':ElementType  T3'
    write(ec_Bluekenu_h,*) ':EndHeader'

    write(ec_Bluekenu_u,*) ':NodeCount', nnt
    write(ec_Bluekenu_u,*) ':ElementCount', nelt
    write(ec_Bluekenu_u,*) ':ElementType  T3'
    write(ec_Bluekenu_u,*) ':EndHeader'

    write(ec_Bluekenu_v,*) ':NodeCount', nnt
    write(ec_Bluekenu_v,*) ':ElementCount', nelt
    write(ec_Bluekenu_v,*) ':ElementType  T3'
    write(ec_Bluekenu_v,*) ':EndHeader'

    do nd=1, nnt
      if ( vdlg_nodes(1,nd) > tolisec ) then
        solu = vdlg_nodes(2,nd)/vdlg_nodes(1,nd)
        solv = vdlg_nodes(3,nd)/vdlg_nodes(1,nd)
      else
        solu = 0.d0
        solv = 0.d0
      endif
      write(ec_Bluekenu_eta,'(3f16.6)') coordonnees(nd,1), coordonnees(nd,2), vdlg_nodes(1,nd)+coordonnees(nd,3)-sol_z_offset
      write(ec_Bluekenu_h,'(3f16.6)')   coordonnees(nd,1), coordonnees(nd,2), vdlg_nodes(1,nd)
      write(ec_Bluekenu_u,'(3f16.6)')   coordonnees(nd,1), coordonnees(nd,2), solu
      write(ec_Bluekenu_v,'(3f16.6)')   coordonnees(nd,1), coordonnees(nd,2), solv
    enddo

    do iel=1, nelt
      write(ec_Bluekenu_eta,*) connectivite(iel,1) , connectivite(iel,2) , connectivite(iel,3)
      write(ec_Bluekenu_h,*) connectivite(iel,1) , connectivite(iel,2) , connectivite(iel,3)
      write(ec_Bluekenu_u,*) connectivite(iel,1) , connectivite(iel,2) , connectivite(iel,3)
      write(ec_Bluekenu_v,*) connectivite(iel,1) , connectivite(iel,2) , connectivite(iel,3)
    enddo

    close(ec_Bluekenu_eta)
    close(ec_Bluekenu_h)
    close(ec_Bluekenu_u)
    close(ec_Bluekenu_v)

  end subroutine bluekenue_export

  subroutine simple_export(ci)

    use precision_kind
    use global_data
    use file_id

    implicit none

    integer, intent(in) :: ci
    integer :: nd, iel, i

    write(valtemps,'(I20)') ci

    if(solvisu==4 .or. solvisu==1) then 
      if(num_mpi_process>1) then
        solfile_simple = trim(adjustl(trim(mpi_process_id_string)))//'_solution_noeuds_simple_'//trim(adjustl(trim(valtemps)))//adjustl(trim('.txt'))
      else
        solfile_simple = 'solution_noeuds_simple_'//trim(adjustl(trim(valtemps)))//adjustl(trim('.txt'))
      end if
      open(unit=ec_sol_simple,file=solfile_simple,status="unknown")
      write(ec_sol_simple,*) tc                        
      do nd=1,nnt
        write(ec_sol_simple,'(4f16.6)') vdlg_nodes(1,nd), vdlg_nodes(1,nd)+coordonnees(nd,3)-sol_z_offset, vdlg_nodes(2,nd), vdlg_nodes(3,nd)
      end do
      close(ec_sol_simple)
    end if

    if(solvisu==5 .or. solvisu==1) then 
      if(num_mpi_process>1) then
        solfile_simple = trim(adjustl(trim(mpi_process_id_string)))//'_solution_elements_simple_'//trim(adjustl(trim(valtemps)))//adjustl(trim('.txt'))
      else
        solfile_simple = 'solution_elements_simple_'//trim(adjustl(trim(valtemps)))//adjustl(trim('.txt'))
      end if
      open(unit=ec_sol_simple,file=solfile_simple,status="unknown")
      write(ec_sol_simple,*) tc                        
      do iel=1,nelt
        write(ec_sol_simple,'(4f16.6)') vdlg(1,iel), vdlg(1,iel)+zm(iel)-sol_z_offset, vdlg(2,iel), vdlg(3,iel)
      end do
      close(ec_sol_simple)
    end if
  end subroutine simple_export

  subroutine cgns_export_end()
    use global_data
    use precision_kind
    use file_id
    use cgns

    implicit none 

    integer :: ierr

    call cgp_close_f(fn_cgw, ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f
  end subroutine cgns_export_end

  subroutine cgns_export_init()
    use global_data
    use precision_kind
    use file_id
    use cgns

    implicit none 

    integer :: ierr
    integer :: n_bases, cellDim, physDim, zone, zoneType, sizes(3*3), ord, i
    integer, dimension(2) :: idata
    character(200) :: basename, zonename

    !!Read cgns file and goto good location
    call cgp_pio_mode_f(0, ierr)

    call cgp_open_f(meshfile_out, CG_MODE_MODIFY, fn_cgw, ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cg_nbases_f(fn_cgw, n_bases, ierr)
    if (ierr .ne. CG_OK) call cg_error_exit_f
    base_cgw = 1

    call cg_base_read_f(fn_cgw, base_cgw, basename, cellDim, physDim, ierr)
    if (ierr .ne. CG_OK) call cg_error_exit_f

    call cg_nzones_f(fn_cgw, base_cgw, n_zones_cgw, ierr)
    if (ierr .ne. CG_OK) call cg_error_exit_f

    do zone=1,n_zones_cgw
      call cg_zone_type_f(fn_cgw, base_cgw, zone, zoneType, ierr)
      if (ierr .ne. CG_OK) call cg_error_exit_f

      call cg_zone_read_f(fn_cgw, base_cgw, zone, zonename, sizes, ierr)
      if (ierr .ne. CG_OK) call cg_error_exit_f

      call cg_goto_f(fn_cgw, base_cgw, ierr, trim(adjustl(zonename)), 0, "end")
      if (ierr .ne. CG_OK) call cg_error_exit_f

      call cg_ordinal_read_f(ord, ierr)
      if (ierr .ne. CG_OK) call cg_error_exit_f

      if(ord == mpi_process_id) then
        exit
      end if
    end do

    my_zone_cgw = zone

    !!Write flow solution pointers
    call cg_biter_write_f(fn_cgw, base_cgw, 'TimeIterValues', visu_snapshots+1, ierr)
    call cg_goto_f(fn_cgw, base_cgw, ierr,'BaseIterativeData_t',1,'end')
    !!RealDouble = 4
    call cg_array_write_f('TimeValues',4,1,visu_snapshots+1,tsolvisu(0:),ierr)

    do zone=1,n_zones_cgw
      call cg_ziter_write_f(fn_cgw,base_cgw,zone,'ZoneIterativeData',ierr)
      call cg_goto_f(fn_cgw,base_cgw,ierr,'Zone_t',zone,'ZoneIterativeData_t',1,'end')
      idata(1)=32
      idata(2)=visu_snapshots+1
      !!Character = 5
      call cg_array_write_f('FlowSolutionPointers',5,2,idata,solname_visu(0:),ierr)
    end do

  end subroutine cgns_export_init

  subroutine cgns_export(it)
    use global_data
    use precision_kind
    use file_id
    use cgns

    implicit none 

    integer, intent(in) :: it

    integer :: sol, zone, ierr, i
    integer :: field_wd, field_vx, field_vy, field_vz, field_mx, field_my, field_mz, field_b, field_fs
    integer, dimension(2) :: idata
    character(200) :: basename, zonename

    !!Read cgns file and goto good location
    do zone=1,n_zones_cgw
      !!!Vertex = 2
      !!!Cellcenter = 3
      call cg_sol_write_f(fn_cgw, base_cgw, zone, solname_visu(it), 3, sol, ierr)
      if (ierr .ne. CG_OK) call cg_error_exit_f

      !!RealDouble = 4
      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'Water Depth', field_wd, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'Bathymetry', field_b, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'Free surface elevation', field_fs, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'MomentumX', field_mx, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'MomentumY', field_my, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'MomentumZ', field_mz, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'VelocityX', field_vx, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'VelocityY', field_vy, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f

      call cgp_field_write_f(fn_cgw, base_cgw, zone, sol, 4, 'VelocityZ', field_vz, ierr)
      if (ierr .ne. CG_OK) call cgp_error_exit_f
    end do

    zone = my_zone_cgw

    !!Not on gpu to save gpu memory
    do i=1,nelt
      vdlg_visu(i,1) = vdlg(1,i)


      if(vdlg_visu(i,1) > tolisec) then
        vdlg_visu(i,2) = vdlg(2,i)
        vdlg_visu(i,3) = vdlg(3,i)
        vdlg_visu(i,4) = 0.
        vdlg_visu(i,5) = vdlg(2,i)/vdlg(1,i)
        vdlg_visu(i,6) = vdlg(3,i)/vdlg(1,i)
        vdlg_visu(i,7) = 0.
      else 
        vdlg_visu(i,2) = 0.
        vdlg_visu(i,3) = 0.
        vdlg_visu(i,4) = 0.
        vdlg_visu(i,5) = 0.
        vdlg_visu(i,6) = 0.
        vdlg_visu(i,7) = 0.
      end if

      vdlg_visu(i,8) = vdlg(1,i) + zm(i) - sol_z_offset
    end do

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_wd, 1, nelt,  vdlg_visu(1,1), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_b, 1, nelt,  zm, ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_fs, 1, nelt,  vdlg_visu(1,8), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_mx, 1, nelt,  vdlg_visu(1,2), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_my, 1, nelt,  vdlg_visu(1,3), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_mz, 1, nelt,  vdlg_visu(1,4), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_vx, 1, nelt,  vdlg_visu(1,5), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_vy, 1, nelt,  vdlg_visu(1,6), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f

    call cgp_field_write_data_f(fn_cgw, base_cgw, zone, sol, field_vz, 1, nelt,  vdlg_visu(1,7), ierr)
    if (ierr .ne. CG_OK) call cgp_error_exit_f
  end subroutine cgns_export

  subroutine paraview_export(ci)
    use precision_kind
    use global_data
    use file_id

    implicit none

    integer, intent(in) :: ci

    integer :: nd, iel
    real(fp_kind) :: solu, solv

    write(valtemps,'(I20)') ci

    solfile_para = trim(adjustl(trim(mpi_process_id_string)))//'_solution-Paraview_'//trim(adjustl(trim(valtemps)))//adjustl(trim('.vtk'))

    open(unit=ec_Paraview,file=solfile_para,status="unknown")

    write(ec_Paraview,'(a)') "# vtk DataFile Version 1.0"
    write(ec_Paraview,'(a)') "2D Unstructured Grid"
    write(ec_Paraview,'(a)') "ASCII"
    write(ec_Paraview,'(a)') ""

    write(ec_Paraview,'(a)') "DATASET UNSTRUCTURED_GRID"
    write(ec_Paraview,'(a,I10,a)') "POINTS", nnt, " float"

    do nd=1, nnt
      write(ec_Paraview,'(3f16.6)') coordonnees(nd,1), coordonnees(nd,2), 0.
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a,I10,I10)') "CELLS", nelt, 4*nelt
    do iel=1, nelt
      write(ec_Paraview,'(I1,3I10)') 3, connectivite(iel,1)-1, connectivite(iel,2)-1, connectivite(iel,3)-1
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a,I10)') "CELL_TYPES", nelt
    do iel=1, nelt
      write(ec_Paraview,'(a)') "5"
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a,I10)') "CELL_DATA", nelt
    write(ec_Paraview,'(a)') "SCALARS eta float"
    write(ec_Paraview,'(a)') "LOOKUP_TABLE default"
    do iel=1, nelt
      write(ec_Paraview,'(f16.6)') vdlg(1,iel)+zm(iel)-sol_z_offset
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a)') "SCALARS h float"
    write(ec_Paraview,'(a)') "LOOKUP_TABLE default"
    do iel=1, nelt
      write(ec_Paraview,'(f16.6)') vdlg(1,iel)
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a)') "SCALARS b float"
    write(ec_Paraview,'(a)') "LOOKUP_TABLE default"
    do iel=1, nelt
      write(ec_Paraview,'(f16.6)') zm(iel)-sol_z_offset
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a)') "VECTORS velocity float"
    do iel=1, nelt
      if ( vdlg(1,iel) > tolisec ) then
        solu = vdlg(2,iel)/vdlg(1,iel)
        solv = vdlg(3,iel)/vdlg(1,iel)
      else
        solu = 0.d0
        solv = 0.d0
      endif
      write(ec_Paraview,'(3f16.6)')   solu, solv, 0.
    end do

    write(ec_Paraview,'(a)') ""
    write(ec_Paraview,'(a)') "VECTORS flux float"
    do iel=1, nelt
      if ( vdlg(1,iel) > tolisec ) then
        write(ec_Paraview,'(3f16.6)')   vdlg(2,iel), vdlg(3,iel), 0.
      else
        write(ec_Paraview,'(3f16.6)')   0., 0., 0.
      endif
    end do

    close(ec_Paraview)

  end subroutine paraview_export

  subroutine save_sol_for_restart
    use global_data
    use file_id
    implicit none

    integer :: iel

    write(ec_sol_elt_t,*) tc                        

    do iel=1,nelt
      write(ec_sol_elt_t,'(4f16.6)') vdlg(1,iel), vdlg(2,iel), vdlg(3,iel)
    enddo

    rewind (ec_sol_elt_t)
  end subroutine save_sol_for_restart
end module pre_post_traitement
